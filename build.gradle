plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'org.sonarqube' version '2.6'
    id 'org.unbroken-dome.test-sets' version '1.4.2'
    id 'com.github.psxpaul.execfork' version '0.1.8'
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'com.dorongold.task-tree' version '1.3'
}

archivesBaseName = 'spring-cloud-ribbon-extensions'
group = 'com.github.enadim'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    jcenterUrl = project.hasProperty('customJcenterUrl') ? project.customJcenterUrl : 'https://jcenter.bintray.com/'
    mavenUrl = project.hasProperty('customMavenUrl') ? project.customMavenUrl : 'https://repo.maven.apache.org/maven2/'
    customSpringMilestoneUrl = project.hasProperty('customSpringMilestoneUrl') ? project.customSpringMilestoneUrl : 'https://repo.spring.io/libs-milestone/'
    mavenPluginsUrl = project.hasProperty('customMavenPluginsUrl') ? project.customMavenPluginsUrl : 'https://plugins.gradle.org/m2/'

    jvm = org.gradle.internal.jvm.Jvm.current()
    javaDescription = '' + jvm
    javaVersion = '' + JavaVersion.current()

    projectUrl = 'https://github.com/enadim/' + archivesBaseName
    projectDescription = 'Netflix ribbon extensions.'
    projectScmUrl = 'scm:git:' + projectUrl + '.git'

    licenseName = 'The Apache License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    authorId = 'enadim'
    authorName = 'Nadim Benabdenbi'
    authorEmail = 'nadim.benabdenbi@gmail.com'

    gitCommitId = 'git rev-parse HEAD'.execute().text.trim()
    gitBranchName = 'git name-rev --name-only HEAD'.execute().text.trim()
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.sonarqube'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'com.github.psxpaul.execfork'
apply from: 'gradle/ide.gradle'
apply from: 'gradle/license.gradle'
apply from: 'gradle/coding.gradle'
apply from: 'gradle/tests.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/artifacts.gradle'
apply from: 'gradle/signing.gradle'
apply from: 'gradle/release.gradle'
apply plugin: 'com.dorongold.task-tree'

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

repositories {
    mavenLocal()
    maven {
        url mavenUrl
    }
    maven {
        url customSpringMilestoneUrl
    }
    jcenter {
        url jcenterUrl
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE'
        mavenBom 'org.springframework.boot:spring-boot:2.0.3.RELEASE'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.20'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    compile("org.springframework.boot:spring-boot-starter-activemq") { ext.optional = true }
    compile("org.apache.activemq:activemq-broker") { ext.optional = true }
    compile("com.fasterxml.jackson.core:jackson-databind") { ext.optional = true }
    //compile('org.springframework.cloud:spring-cloud-starter-config') { ext.optional = true }
    compile('org.springframework.cloud:spring-cloud-starter-netflix-ribbon') { ext.optional = true }
    compile('org.springframework.cloud:spring-cloud-starter-openfeign') { ext.optional = true }
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client') { ext.optional = true }
    compile('org.springframework.cloud:spring-cloud-starter-netflix-zuul') { ext.optional = true }
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-server') {
        exclude group: 'javax.servlet', module: 'servlet-api'
        ext.optional = true
    }
    compile('org.apache.activemq:artemis-jms-server') { ext.optional = true }
    compile("org.springframework.boot:spring-boot-starter-activemq") {
        ext.optional = true
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jms_1.1_spec'
    }

    testCompileOnly 'org.projectlombok:lombok:1.16.20'
    testCompile 'org.springframework.boot:spring-boot-starter-web'
    testCompile 'org.springframework.boot:spring-boot-test'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.mockito:mockito-core:2.18.0'
    testCompile 'net.bytebuddy:byte-buddy:1.8.5'
    testCompile 'io.rest-assured:rest-assured:3.0.7'

    examplesCompileOnly 'org.projectlombok:lombok:1.16.20'
}

compileJava {
    options.fork = true
}
compileJava.group 'build'
compileTestJava.group 'build'
processResources.group 'build'
sourcesJar.group 'build'
javadocJar.group 'build'
testSourcesJar.group 'build'
testJar.group 'build'
processTestResources.group 'build'
install.group 'build'
dependencyReport.group 'report'
htmlDependencyReport.group 'report'
taskReport.group 'report'
propertyReport.group 'report'

allprojects {
    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}